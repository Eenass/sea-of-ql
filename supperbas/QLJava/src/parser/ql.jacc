%package parser
%class QLParser
%interface QLTokens
%semantic ast.ASTNode 
%start top

%token <ast.expression.value.Int> INT
%token <ast.expression.value.Money> MONEY
%token <ast.expression.value.Bool> BOOL
%token <ast.expression.value.Str> STR
%token <ast.expression.value.Ident> IDENT
%token '*' '/' '+' '-' OR AND NEQ EQ '<' LEQ '>' GEQ '!'
%token '(' ')' '{' '}'
%token IF ELSE

%left OR
%left AND
%left '>' '<' GEQ LEQ NEQ EQ
%left '+' '-'
%left '*' '/'
%left UMIN UPLUS UNOT

%type <ast.Expression> expr

%{
import ast.Expression;
import ast.expression.binary.*;
import ast.expression.unary.*;
import ast.expression.value.*;
@SuppressWarnings("unused")
%}

%%

 top : expr { result = $1; }
     ;

 expr	 : '+' expr	%prec UPLUS { $$ = new Pos($2); }
		 | '-' expr	%prec UMIN  { $$ = new Neg($2); }
		 | '!' expr %prec UNOT  { $$ = new Not($2); }
		 | expr '*' expr		{ $$ = new Mul($1, $3); }
		 | expr '/' expr 		{ $$ = new Div($1, $3); }
		 | expr '+' expr		{ $$ = new Add($1, $3); }
		 | expr '-' expr		{ $$ = new Sub($1, $3); }
		 | expr EQ expr		    { $$ = new Eq($1, $3); }
		 | expr NEQ expr		{ $$ = new NEq($1, $3); }
		 | expr '>' expr		{ $$ = new GT($1, $3); }
		 | expr '<' expr		{ $$ = new LT($1, $3); }
		 | expr GEQ expr		{ $$ = new GEq($1, $3); }
		 | expr LEQ expr		{ $$ = new LEq($1, $3); }
		 | expr AND expr	    { $$ = new And($1, $3); }
	     | expr OR expr  		{ $$ = new Or($1, $3); }
		 | INT					{ $$ = $1; }
		 | MONEY				{ $$ = $1; }
		 | BOOL					{ $$ = $1; }
		 | STR					{ $$ = $1; }
		 | IDENT         		{ $$ = $1; }
		 | '(' expr ')'			{ $$ = $2; }
		 ;
		 

%%
private QLLexer lexer; 

private Expression result;

public Expression getResult() {
  return result;
}

public QLParser(QLLexer lexer) { 
  this.lexer = lexer; 
}

private void yyerror(String msg) { 
  System.err.println(msg); 
}